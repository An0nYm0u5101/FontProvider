buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.2'
    }
}
import java.util.function.Consumer

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "moe.shizuku.fontprovider"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        String mapValue = 'new java.util.HashMap<String, Integer>() {{'
        fileTree("src/main/assets").files.forEach(new Consumer<File>() {
            @Override
            void accept(File file) {
                mapValue += 'put("' + file.name + '", ' + file.size() + ');'
            }
        })
        mapValue += '}}'

        buildConfigField ("java.util.Map<String, Integer>", "BUILT_IN_FONTS_SIZE", mapValue)
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            signingConfig signingConfigs.sign
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:support-annotations:$supportLibraryVersion"
    implementation "com.android.support:support-fragment:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation project(path: ':api')
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'moe.shizuku.support:recyclerview-utils:2.0'
    implementation 'moe.shizuku.support:support-utils:2.0'
    implementation "moe.shizuku.preference:preference:$preferenceLibraryVersion"
    implementation "moe.shizuku.preference:preference-dialog-android:$preferenceLibraryVersion"
    implementation "moe.shizuku.preference:preference-simplemenu:$preferenceLibraryVersion"
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.0@aar') {
        transitive = true
    }
}

apply from: rootProject.file('signing.gradle')
